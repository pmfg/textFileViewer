apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "pt.pedro.textfileviewer"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "v1.0.0 (" + gitBranch() + "@" + gitSHA() + ")"
        resValue "string", "gitBranch", gitBranch()
        resValue "string", "gitSHA", gitSHA()
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
}

static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

static def gitSHA() {
    def sha = ""
    def proc = "git rev-parse --short HEAD".execute()
    proc.in.eachLine { line -> sha = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    sha
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:28.0.0'
}
